plugins {
    id 'com.android.library'
    id 'maven'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


def configurePom(def pom) {
    pom.project {
        name('HubAppHost')
        artifactId('hubapphost')
        packaging('aar')
        description("A framework for building apps over React native")
        url("https://github.com/mganandraj/hubapphost.git")

        scm {
            url("https://github.com/mganandraj/hubapphost.git")
            connection("scm:git:https://github.com/mganandraj/hubapphost.git")
            developerConnection("scm:git:git@github.com:mganandraj/hubapphost.git")
        }

        licenses {
            license {
                name("MIT License")
                url("https://github.com/mganandraj/hubapphost/blob/master/LICENSE")
                distribution("repo")
            }
        }

        developers {
            developer {
                id("microsoft")
                name("Microsoft")
            }
        }
    }
}

afterEvaluate { project ->

    version = '0.0.1'
    group = 'com.microsoft'

    task installArchives(type: Upload) {
        configuration = configurations.archives
        repositories.mavenDeployer {
            repository url: "file://${projectDir}/../android"
            configurePom pom
        }
    }
}

dependencies {

    implementation "com.facebook.react:react-native:${versions.reactNative}"

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation "androidx.work:work-runtime:${versions.androidx_work}"
    implementation "com.nimbusds:nimbus-jose-jwt:${versions.nimbus_jose_jwt}"

    implementation "com.google.dagger:dagger:${versions.dagger}"
    implementation "com.google.dagger:dagger-android:${versions.dagger}"

    annotationProcessor "com.google.dagger:dagger-compiler:${versions.dagger}"
    annotationProcessor "com.google.dagger:dagger-android-processor:${versions.dagger}"

    implementation "androidx.room:room-runtime:${versions.androidx_room}"
    annotationProcessor "androidx.room:room-compiler:${versions.androidx_room}"

    implementation "com.google.guava:guava:${versions.guava_android}"
    implementation "com.google.code.gson:gson:${versions.gson}"
    implementation "com.parse.bolts:bolts-tasks:${versions.bolts_tasks}"

    // implementation "com.pixplicity.easyprefs:library:${versions.easyprefs}@aar"

    // App Center SDK
    implementation "com.microsoft.appcenter:appcenter-crashes:${versions.appcenter_crashes}"
    implementation "com.microsoft.appcenter:appcenter-distribute:${versions.appcenter_distribute}"
    implementation "com.github.anrwatchdog:anrwatchdog:${versions.anrwatchdog}"

}